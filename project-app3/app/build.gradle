plugins {
    id 'java'
    id 'eclipse-wtp' //이클립스 웹 프로젝트 다룰 수 있는 기능 추가
    id 'war' // 웹애플리케이션 배표파일(.war)을 생성할 수 있는 기능 추가
    id "io.freefair.lombok" version "6.4.1"
}

repositories {
    mavenCentral()
}

// CLI에서 빌드 할 때 사용할 설정 정보
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = '11'
    targetCompatibility = '11'
}


// Eclipse IDE에서 빌드할 때 사용할 설정 정보
eclipse {
    project {
        name = "project-app3"
    }
    jdt {
      sourceCompatibility = 11
      targetCompatibility = 11
      javaRuntimeName = "JavaSE-11"
    }
    // 자바 웹 개발 도구에서 사용할 값을 설정한다.
    wtp {
        facet {
            // 웹애플리케이션을 실행할 컨테이너의 자바 버전을 설정한다. 
            facet name: 'jst.java', version: '11'
        }
        facet {
            // 웹애플리케이션을 실행할 컨테이너의 Servlet/JSP 버번을 설정한다.
            facet name: 'jst.web', version: '4.0'
        }
        component {
            // 웹애플리케이션의 컨텍스트 루트 경로를 설정한다.
            // => 즉 이 웹 애플리케이션에 접근할 때 '/'를 사용할 것이라는 의미이다.
            contextPath = '/'

            //you can configure the deployName:
            //deployName = 'web'
        }
    }
}

// 웹 애플리케이션 배포 파일(.war)을 만들 때 사용할 기본 파일 이름을 설정한다.
// 이 이름은 웹 애플리케이션 context root 경로로도 사용된다.
war {
    archiveBaseName = "web"
}

    


dependencies {
    // Servlet API 라이브러리
    // => 컴파일 할때만 사용하고, 배포할 때는 제외할 라이브러리
    providedCompile 'javax.servlet:javax.servlet-api:4.0.1'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.3'
    implementation 'org.mybatis:mybatis:3.5.9'
    implementation 'javax.servlet:jstl:1.2'
    implementation 'org.springframework:spring-webmvc:5.3.19'
    
    // Spring에서 JDBC를 다룰때 필요한 라이브러리
    implementation 'org.springframework:spring-jdbc:5.3.19'   
    implementation 'org.mybatis:mybatis-spring:2.0.7'
}
